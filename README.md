# Servidor de impresion, como servicio. para windows.

Todos los archivos de creacion de exe estan en este Repositorio [https://github.com/cesaredm/printServerExe](https://github.com/cesaredm/printServerExe)

Para este proyecto se esta usando apache common daemon para crear el servicio
Se estan usando las siguientes tecnologias.

- Gson
- EscPos de anastaciocintra
- Apache common daemon
- Procrun - prunsrv.exe para crear el servicio en windows
- InnoSetup
- Launch4j - Creador de wrapper exe a partir del jar
- Amazon Corretto 23.0.2 JDK para desarrollo
- JRE de Adoptium [https://adoptium.net/es/temurin/releases/?os=windows&arch=x64&package=jre&version=23](Link_descarga)
  aunque hay alternativas, en mi caso use esta por mas liviana.

#### JRE-JDK de OpenJdk

- Amazon Corretto
- Zulu
- AdoptOpenJDK (usada actualmente)
- Bellsoft - [https://bell-sw.com/pages/downloads/?version=java-23&os=windows](https://bell-sw.com/pages/downloads/)

### parametros usados

> Creacion del servicio

```cmd
./prunsrv.exe //IS//PrintServer --DisplayName="Servidor de impresion" --Description="Servidor de impresion de cdsfot" --Startup=auto --StartMode=exe --StartImage=E:/direccion_completa_al_exe/PrintServer.exe --StopMode=exe --StopImage=E:/direccion_completa_al_exe/PrintServer.exe --StopParams=stop --ServiceUser=LocalSystem --StartPath=E:/direccion_completa_al_directorio_exe/servidor_impresion/ --LogPath=E:/direccion_completa_al_directorio_exe/servidor_impresion/logs --StdOutput=E:/direccion_completa_al_directorio_exe/servidor_impresion/logs/stdOut.log --StdError=E:/direccion_completa_al_directorio_exe/servidor_impresion/logs/stdErr.log --StopPath=E:/direccion_completa_al_exe/PrintServer.exe
```

### InnoSetup
En InnoSetup para la creacion del Instalador
- Documentacion - [https://jrsoftware.org/ishelp/index.php](https://jrsoftware.org/ishelp/index.php)

```pascal
; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=PrintServer
AppVersion=0.5
DefaultDirName={pf}\PrintServer
DefaultGroupName=CDsoft
OutputBaseFilename=PrintServerSetup
Compression=lzma
SolidCompression=yes
PrivilegesRequired=admin

[Languages]
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[CustomMessages]
spanish.WelcomeLabel1=¡Bienvenido al instalador de PrintServer un servicio de impresión creado por CDsoft.!
spanish.ExitSetupMessage=¿Está seguro de que desea cancelar la instalación de PrintServer?
spanish.KeepDataMsg=¿Desea mantener las carpetas de configuraciones.? Si selecciona "No", las carpetas y todos sus contenidos serán eliminados permanentemente.

[Files]
; mi app .exe
Source: "E:\cesar\Documents\servidor_impresion\PrintServer.exe"; DestDir: "{app}"; Flags: ignoreversion
; Eclipse JRE
Source: "E:\cesar\Documents\servidor_impresion\OpenJDK23U-jre_x64_23.0.2.msi"; DestDir: "{tmp}"; Flags: ignoreversion deleteafterinstall
; apache common daemon
Source: "E:\cesar\Documents\servidor_impresion\prunsrv.exe"; DestDir: "{app}"; Flags: ignoreversion
; la flags onlyifdoesntesist es para que no sobrescriba el archivo si ya existe, ya que ignoreversion sobrescribe los archivos mas viejos por los nuevos
Source: "E:\cesar\Documents\servidor_impresion\printers.properties"; DestDir: "C:\impresorasConfig\"; Flags: onlyifdoesntexist uninsneverunistall 
; Archivos de Caddy, instalados en una carpeta separada
Source: "E:\cesar\Documents\servidor_impresion\caddy\caddy.exe"; DestDir: "C:\Caddy\"; Flags: onlyifdoesntexist uninsneverunistall 
Source: "E:\cesar\Documents\servidor_impresion\caddy\Caddyfile"; DestDir: "C:\Caddy\"; Flags: onlyifdoesntexist uninsneverunistall

[Run]
; Instalar JRE de OPENjdk Adoptium la version
Filename: "msiexec.exe"; Parameters: "/i ""{tmp}\OpenJDK23U-jre_x64_23.0.2.msi"""; Description: Instalando JRE; Flags: waituntilterminated
; Registrar el servicio de Printserver al finalizar la instalación del JRE
Filename: "{app}\prunsrv.exe"; Parameters: "//IS//PrintServer --DisplayName=""Servidor de Impresión"" --Description=""Servicion de impresion CDsoft"" --Startup=auto --StartMode=exe --StartImage=""{app}\PrintServer.exe"" --StartPath=""{app}"" --StopMode=exe --StopImage=""{app}\PrintServer.exe"" --StopParams=stop --StopPath=""{app}\PrintServer.exe"" --ServiceUser=LocalSystem --LogPath=""{app}\log"" --LogLevel=Debug --StdOutput=""{app}\log\stdOutput.log"" --StdError=""{app}\log\stdError.log"""; WorkingDir: "{app}"; Flags: runhidden
; correr el servicio de printserver
Filename: "net"; Parameters: "start PrintServer"; Flags: runhidden
; Instalar Caddy como servicio
Filename: "sc.exe"; Parameters: "create caddy start= auto binPath= ""C:\Caddy\caddy.exe run"""; Flags: runhidden
; Iniciar Caddy como servicio, al iniciar Caddy crea y guarda los certificados CA en C:\ProgramData\Caddy
Filename: "sc.exe"; Parameters: "start caddy"; Flags: runhidden

; agrgar regla de firewall para el servidor en el puerto 8088 (esto por ahora no es necesario, ya que usamos caddy)
;Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=PrintServer dir=in action=allow protocol=TCP localport=8088"; Flags: runhidden
;Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=PrintServer dir=out action=allow protocol=TCP localport=8088"; Flags: runhidden

[Dirs]
; Asegúrate de que la carpeta de Caddy exista, y si no existen crearlas, uninsneverunistall para que al desinstalar no elimine las carpetas, la eliminacion pasaria a la logica de CODE mas abajo.
Name: "C:\Caddy\"; Flags: uninsneveruninstall
Name: "C:\impresorasConfig\"; Flags: uninsneveruninstall

[UninstallRun]
; Detener y eliminar el servicio durante la desinstalación
Filename: "{app}\prunsrv.exe"; Parameters: "//SS//PrintServer"; Flags: runhidden
Filename: "{app}\prunsrv.exe"; Parameters: "//DS//PrintServer"; Flags: runhidden
Filename: "sc.exe"; Parameters: "stop caddy"; Flags: runhidden
Filename: "sc.exe"; Parameters: "delete caddy"; Flags: runhidden
;Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=PrintServer"; Flags: runhidden

[Code]
// Define la función para preguntar al usuario durante la desinstalación si eliminamos los directorios creados cuando se instalo.
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  // Verificamos si estamos en el paso de desinstalación real.
  if CurUninstallStep = usUninstall then
  begin
    // Evitamos mostrar la solicitud en modo silencioso para prevenir cuelgues.
    if not UninstallSilent then
    begin
      // Muestra una caja de mensaje con una pregunta de "Sí/No".
      if MsgBox(ExpandConstant('{cm:KeepDataMsg}'), mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDNO then
      begin
        // Si el usuario elige "No", procede a eliminar la carpeta de datos.
        // DelTree(path, IsDir, DeleteFiles, DeleteSubdirsAlso)
        // El segundo parámetro "True" indica que es un directorio.
        // Los dos últimos "True" indican que elimine archivos y subdirectorios.
        DelTree(ExpandConstant('C:\Caddy'), True, True, True);
        DelTree(ExpandConstant('C:\impresorasConfig'), True, True, True);
      end;
    end;
  end;
end;
```

### Explicación de los parámetros usados:

- //IS//PrintServer: Registra el servicio con el nombre "PrintServer".

- --Description: Descipcion del servicio.

- --DisplayName: Nombre del servicio
- --Startup: forma de arranque automatico
- --StartMode: forma de iniciar el servicio en este caso exe, por que tengo un exe, si fuera un jar seria jvm
- --StartImage: Imagen que ejecutara, se le pasa la ruta del exe
- --StopMode: forma de parar el servicio, en este caso exe
- --StopImage: direccion de el exe
- --StopParams: parametros de parar el servicio en este caso stop, que esta en mi clase main
- --ServiceUser: permiso, es este caso como administrador que es LocalSystem
- --StartPath: darle permiso a la carpeta donde nesecita trabajar.
- --LogPath: Direccion donde estaran los logs
- --StdOutput: direccion de LogPath mas el nombre del archivo ejemplo `stdOut.log`
- --StdError: direccion de LogPath mas el nombre del archivo ejemplo `stdErr.log`
- --LogLevel: puede ser `Debug` `Info` `Error`

### Una vez creado el servicio se gestionan de la siguiente forma

- Iniciar servicio

```cmd
net start PrintServer
```

- Detener servicio

```cmd
net stop PrintServer
```

- Eliminar servicio ejecutando esto dentro de la carpeta de instalacion en C:/Archivos de programa(x86)/printserver

```cmd
./prunsrv.exe //DS//PrintServer
```

> Esta parte no se usara, pero puede ser usado en el futuro, aunque ahora mismo no funciona.

### Caddy 
Caddy como servidor proxy inverson, por que nos proporciona certificados autofirmados para comunicaion local con HTTPS 


### jlink

se uso jlink que es una herramiento que viene en el jdk, que sirve para crear un JVM custom con lo necesario que nesecita nuestra app o jar para funcionar.
para saber que dependencias nesecita mi app corro el siguiente comando

```cmd
jdeps --print-module-deps --ignore-missing-deps -recursive PrintServer-1.0-SNAPSHOT-jar-with-dependencies.jar
```

esto devuelve por ejemplo en mi caso

```cmd
java.base
```

entonces solo eso nesecito.

- Para crear el runtime custom corro el siguiente comando

```cmd
jlink --module-path "C:\Program Files\Amazon Corretto\jdk23.0.2_7\jmods" --add-modules java.base --output runtime
```

### configuracion de firewall

esto para que el firewall permita entradas a este puerto del servidor, en caso de ser necesario
se Incluyo en el script de instalacion.

```cmd
netsh advfirewall firewall add rule name="Allow Port 8088" dir=in action=allow protocol=TCP localport=8088
```
# Documentacion de uso

## Archivo de configuracion de impresoras

el servidor trabaja con un archivo de configuracion de impresoras llamadao printers.properties donde se define la configuracion de las impresoras
de la siguiente manera, ocupa los siguientes campos por ahora.

- cocina.ip: que lleva la ip de la impresora en la red
- cocina.port: el puerto de la impresora en este caso el puerto raw normamente siempre es 9100
- cocina.copias: numero de copias que se nesecitan, si no ocupo el valor debe ser 0
- cocina.logo: (Opcional) nombre del logo si se usara uno local, este debe estar ubicado dentro de la carpeta de **impresorasConfig** 
- cocina.papelSize: (Opcional) normalmente es de 48, 32, o 42
- cocina.tipoConexion: para indicar que conexion se usara, si red o usb el valor debe ser red o usb

```properties
cocina.ip=192.168.1.45
cocina.port=9100
cocina.copias=0
cocina.tipoConexion=red
#cocina.logo=logo.png
#cocina.papelSize=48

#aqui mas impresoras
#otra.ip=192.168.1.41
#otro.port=9100
#otro.copias=0
#otro.tipoConexion=usb
```
## Rutas api
- Imprimir factura **POST**
**url:8088/print/{nombre_de_impresora}** : Imprimir factura
- Imprimir comanda **POST**
**url:8088/comanda/print/{nombre_de_impresora}** : Imprimir Comanda
- Listar impresoras instaladas **GET**
**url:8088/impresoras** : Listar las impresoras instaladas
- Impresion de test **GET**
**url:8088/prueba/{nombre_de_impresora}** : Impresion de test de impresion
